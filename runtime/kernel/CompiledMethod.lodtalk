"CompiledMethod"
"Most of the method have been taken from Pharo"

self class: CompiledMethod.
self category: 'accessing'.

self method [
initialPC
    ^ (self numLiterals + 1) * Smalltalk wordSize
].

self method [
numArgs
    ^ (self header bitShift: -24) bitAnd: 16r0F
].

self method [
numLiterals
    ^ self header bitAnd: 16r7FFF
].

self method [
numTemps
    ^ (self header bitShift: -18) bitAnd: 16r3F
].

self method [
primitive
    | initialPC |
    ^ (self header anyMask: 65536)
        ifTrue: [
            initialPC := self initialPC.
            (self at: initialPC + 1) + ((self at: initialPC + 2) bitShift: 8)
        ] ifFalse: [
            0
        ]
].

self method [
selector
    | penultimateLiteral |
    penultimateLiteral := self penultimateLiteral.
    ^ penultimateLiteral isMethodProperties
        ifTrue: [ penultimateLiteral selector ]
        ifFalse: [ penultimateLiteral ]
].

self category: 'literals'.
self method [
objectAt: index
    <primitive: 68>
    self primitiveFailed
].

self method [
objectAt: index put: value
    <primitive: 69>
    self primitiveFailed
].

self method [
header
    ^ self objectAt: 1
].

self method [
literalAt: index
    ^ self objectAt: index + 1
].

self method [
literalAt: index put: value
    ^ self objectAt: index + 1 put: value
].

self category: 'private'.
self method [
penultimateLiteral
    | index |
    index := self numLiterals - 1.
    ^ index > 0 ifTrue:
        [self literalAt: index]
        ifFalse: [0]
].
